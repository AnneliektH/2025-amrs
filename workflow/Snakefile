import os
import pandas as pd

# def ksizes
KSIZE = 21
# KSIZE = 25
# def outdir
OUTPUT_DIR ="/group/ctbrowngrp2/amhorst/2025-amrs/results/rgi"
ATLAS_DIR = "/group/ctbrowngrp2/scratch/annie/2023-swine-sra/results/atlas"
SIGDIR = "/group/ctbrowngrp2/scratch/annie/2024-pigparadigm/results/sourmash/sketches/read_s100"
logs_dir ="/group/ctbrowngrp2/amhorst/2025-amrs/results/logs"


# set configfile
configfile: "../config/config.yaml" 
# Set samples for human and pig
metadata_metag = pd.read_csv(config['metag'], usecols=['acc'])
# Create a list of run ids
samples_pig = metadata_metag['acc'].tolist()
# Define samples
PIG_METAG = config.get('samples', samples_pig)


rule all:
    input:
        #expand('../results/rgi/check/{metag}.rgibwt.done', metag=PIG_METAG),
        expand('../results/smash/fastgather/{metag}.{ksize}.100.csv', metag=PIG_METAG, ksize=KSIZE)

rule rgi_bwt:
    input:
        read_one=f"{ATLAS_DIR}/atlas_{{metag}}/{{metag}}/sequence_quality_control/{{metag}}_QC_R1.fastq.gz",
        read_two=f"{ATLAS_DIR}/atlas_{{metag}}/{{metag}}/sequence_quality_control/{{metag}}_QC_R2.fastq.gz",
    output:
        check = "../results/rgi/check/{metag}.rgibwt.done",
    params:
        output_prefix=f"{OUTPUT_DIR}/rgi_{{metag}}/rgi_{{metag}}"
    benchmark: f"{logs_dir}/rgi.{{metag}}.benchmark"
    conda: 
        "rgi"
    threads: 15
    shell: 
        """
        mkdir -p $(dirname {params.output_prefix})
        rgi bwt \
        --read_one {input.read_one} \
        --read_two {input.read_two} \
        --output_file {params.output_prefix} \
        --local --clean -n {threads} && touch {output.check}
        """


rule fastgather:
    input:
       sig = f"{SIGDIR}/{{metag}}.sig.gz",
       db = '../resources/database/CARD_smash/card_protein_homolog_model.nucleotide.zip'
    output:
        csv = "../results/smash/fastgather/{metag}.{ksize}.100.csv",
    conda: 
        "branchwater-skipmer"
    threads: 15
    benchmark: f"{logs_dir}/fastgather.{{metag}}.{{ksize}}.1000.benchmark"
    shell:
        """ 
        sourmash scripts fastgather \
        {input.sig} {input.db} \
        -k {wildcards.ksize} --scaled 100 -t 0 \
        -c {threads} -o {output.csv}
        """